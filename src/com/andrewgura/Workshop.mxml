<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:mx="library://ns.adobe.com/flex/mx"
         creationComplete="onCreationComplete(event)"
         resize="onResize(event)"
         styleName="editor">

    <fx:Script><![CDATA[
        import com.andrewgura.controllers.MainController;
        import com.andrewgura.controllers.ShortcutsController;
        import com.andrewgura.models.MainModel;
        import com.andrewgura.ui.components.Editor;
        import com.andrewgura.ui.popup.PopupFactory;
        import com.andrewgura.vo.WorkshopConfigVO;

        import mx.core.FlexGlobals;
        import mx.events.FlexEvent;
        import mx.events.MenuEvent;
        import mx.events.ResizeEvent;

        import spark.components.WindowedApplication;

        [Bindable]
        private var mainModel:MainModel = new MainModel();

        [Bindable]
        public var config:WorkshopConfigVO;

        [Bindable]
        public var editor:Editor;

        private function onCreationComplete(event:FlexEvent):void {
            mainModel.config = config;
            editor = new mainModel.config.editorClass();
            editor.includeInLayout = false;
            editor.width = editorHolder.width - 10;
            editor.height = editorHolder.height - 10;
            editor.x = 5;
            editor.y = 5;
            editorHolder.addElement(editor);
            MainController.initApplication(mainModel);
            var application:WindowedApplication = WindowedApplication(FlexGlobals.topLevelApplication);
            application.showStatusBar = false;
            application.addEventListener(InvokeEvent.INVOKE, onAppInvoke);
            if (application.stage) {
                application.stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown);
            } else {
                application.addEventListener(Event.ADDED_TO_STAGE, onAppAddedToStage);
            }
        }

        private function onAppAddedToStage(event:Event):void {
            event.target.removeEventListener(Event.ADDED_TO_STAGE, onAppAddedToStage);
            event.target.stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown);
        }

        private function onAppInvoke(event:InvokeEvent):void {
            if (event.arguments.length > 0) {
                var fileName:String = event.arguments[0];
                MainController.openFileByName(fileName);
            }
        }

        private function onKeyDown(event:KeyboardEvent):void {
            if (event.ctrlKey) {
                if (event.keyCode == Keyboard.CONTROL) {
                    return;
                }
                ShortcutsController.doCtrlShortcutAction(event.keyCode);
            }
        }

        private function onMenuItemClick(event:MenuEvent):void {
            ShortcutsController.doMenuItemAction(event.item);
        }

        protected function onResize(event:ResizeEvent):void {
            PopupFactory.instance.centerAllPopups();
        }
        ]]>
  </fx:Script>

    <fx:Binding source="mainModel.currentProject" destination="editor.project"/>
    <fx:Binding source="editorHolder.width-10" destination="editor.width"/>
    <fx:Binding source="editorHolder.height-10" destination="editor.height"/>

    <s:VGroup width="100%" height="100%" gap="0">
        <mx:MenuBar id="menuBar" width="100%" height="24" top="0"
                    dataProvider="{mainModel.menuDataProvider}" showRoot="false" labelField="label"
                    styleName="breezeMenuBar" menuStyleName="breezeMenu"
                    itemClick="onMenuItemClick(event)"/>

        <s:TabBar dataProvider="{mainModel.openedProjects}" labelField="name"
                  selectedIndex="@{mainModel.currentProjectIndex}"
                  visible="{mainModel.openedProjects.length>1}" includeInLayout="{mainModel.openedProjects.length>1}"/>

        <s:Group id="editorHolder"
                 width="100%" height="100%"/>

    </s:VGroup>

</s:Group>
